<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.6">
  <POU Name="FB_NciMFunctions" Id="{d05c143a-906f-47b9-b2b8-8985f0106a2e}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_NciMFunctions
VAR_IN_OUT
	sNciToPlc		: NCTOPLC_NCICHANNEL_REF;
	sPlcToNci		: PLCTONC_NCICHANNEL_REF;
END_VAR
VAR_OUTPUT
	bBusy			: BOOL;
	bError			: BOOL;
	nErrorId		: UDINT;
END_VAR
VAR
	fbConfirm		: ItpConfirmHsk;
	nAktMfunc		: INT :=  -1;
	bConfirm		: BOOL;
	i				: INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
(* Check for m function and store information in bMFunc Array	*)
IF ItpIsHskMFunc(sNciToPlc:= sNciToPlc) THEN
	nAktMfunc 			:= ItpGetHskMFunc(sNciToPlc:= sNciToPlc);
	bMFunc[nAktMfunc]	:= TRUE;
ELSE
	nAktMfunc 	:= -1;
	bConfirm 	:= FALSE;
	FOR i := 0 TO cnMaxMFuncNumber DO
		bMFunc[i]		:= FALSE;
		bQuitMFunc[i]	:= FALSE;
	END_FOR
END_IF

(* Confirm M function, if bit in array bQuitMFunc is set *)
IF nAktMfunc <> -1 THEN
	IF bQuitMFunc[nAktMfunc] THEN
		bConfirm	:= TRUE;
	END_IF
	bQuitMFunc[nAktMfunc]	:= FALSE;
END_IF

(* Confirm M function	*)
fbConfirm(
	bExecute	:= bConfirm, 
	sNciToPlc	:= sNciToPlc, 
	sPlcToNci	:= sPlcToNci, 
	bBusy		=> bBusy, 
	bErr		=> bError, 
	nErrId		=> nErrorId);]]></ST>
    </Implementation>
    <LineIds Name="FB_NciMFunctions">
      <LineId Id="3" Count="28" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>